#|
 This file is a part of ABCD
 (c) 2015 Shirakumo http://tymoon.eu (shinmera@tymoon.eu)
 Author: Nicolas Hafner <shinmera@tymoon.eu>
|#

(in-package #:org.shirakumo.abcd)

(defun ensure-list (thing)
  (if (listp thing)
      thing
      (list thing)))

(defun externalize (thing)
  (typecase thing
    (pathname (uiop:native-namestring thing))
    (string thing)
    (T (princ-to-string thing))))

(defmacro shellify (&body options)
  `(format NIL ,(format NIL "累蔺磲疸狎＇骈蝮镳糸镱螬括狃痨＇狃疱钿磲疸狎＇蝈篝镳糸镱螬┅ㄤ彐磲泸鏖翳沆遽铄洵骈戾è骈戾骘蝽怙澌怙澌啜戾è骈戾骘蝽┅躅鏖钿痱雉邈痱镧棱镤磲疸狎＇蹰镳轰屐弭瀛骈戾殒屮轶趔骈戾螬┅ㄤ彐躅磲脲狍滏鲩箝忪簌礅镬ㄩ眇矧簌礅镬横箐姣轭翦蜴徙濠ㄥ痫螋簌礅镬横箐姣轭翦蜴徙濠ㄥ痫螋簌礅镬横箐姗ㄤ彐磲泸溴骈铄狍滏轭翦蜴徙瀛沆狍钺礤溟蝈泗篚疱蜚灬篌弩溟蝈泗箪雉蝈篝镳糸镱螬啜痱镧磲脲狍滏鲩箝忪К钺礤ㄤ彐沆狍钺礤溟蝈泗篚疱蜚灬篌弩溟蝈泗箪雉里痿轱铙┅ㄤ彐磲泸溴骈铄狍滏轭翦蜴徙瀛骢钽糸镱钺礤狎珞怙澌骘蝽螬啜痱镧磲脲狍滏鲩箝忪К钺礤ㄤ彐躅钺礤狎珞梨矧眢┅ㄤ彐躅痱镢弩箦洵骈戾疳翳钺礤ㄦ戾è豉疱豉疱篝蜷铉羼踽疳翳钺礤豉疱疳翳钺礤豉疱┅磲脲疳翳钺礤呼疱ㄣ镱è矧豉疱沣豉疱沭豉疱泺豉疱沭稷豉疱惬┅㈤棰è矧豉疱恽┅㈤ㄔ麽蝾⒄铍铒黝骈戾豉疱麸痱屦蝻沐篌幄疳翳钺礤豉疱疳翳钺礤┅㈤┅轰彐狨祠疳翳钺礤┅ㄤ彐躅狍箦礅戾洵骈戾疳翳钺礤磲脲疳翳钺礤呼疱铫轰彐狨祠疳翳钺礤┅ㄤ彐躅狎汨轹瀛骈戾疳翳钺礤磲脲疳翳钺礤呼疱⑨轰彐狨祠疳翳钺礤┅ㄤ彐躅箬狎邃镡赍泗骈戾疳翳钺礤磲脲疳翳钺礤呼疱＋躅轼Ⅲ铫＋鏖钿秣潇膦轰彐狨祠疳翳钺礤┅ㄤ彐鲠铒翳轭绐磲脲簌礅镬⑽显壬吻┅ㄤ彐躅礤蜱瀛镳糸镱镳糸镱镳糸镱犰溴驷蹯趔换湾蜱祜镳骘镳糸镱鲠祯濠镱溴驷蹯趔怡＇沅潋骘屮鲠ㄧ弭镳糸镱镳糸镱铒翳轭绐滹躅戾篌篝蜷铉镳糸镱哄钿暴ㄣ镱è羼屮鲠铒翳轭绐瘐箬鲠祯镳糸镱螬瘐箬镳糸镱镳糸镱螬è犷扉篝鲠祯濠扉篝屮鲠飑ㄤ镬轶ㄩ翦蝈鲥蝮鲠祯濠瘐箬铄轸屙ㄧ弭镳糸镱镳糸镱呼弩＇羼踽飑┅┅换序镢弩蝈盹鲠祗祜镳骘镳糸镱鲠祯濠镱镳糸镱怡＇沅潋滹麒孱篝蜷铉镳糸镱哄钿暴戾è蝈犰镳糸镱ㄦ轭洵簌礅镬篚怏羼篝蜷铉镳糸镱暴⑺刨紫夷┅ㄣ镱è羼鲠祯冤蝈礞镳糸镱蝈犰镳糸镱┅è扉篝鲠祯濠箦翩ㄧ弭镳糸镱蝈犰镳糸镱蝈盹鲥殒灬礅溽ㄡㄦ轭鲠祯呼弩＇羼踽飑ㄧ弭镳糸镱蝈犰镳糸镱┅┅┅蝈礞镳糸镱镳糸镱┅镳糸镱螬ㄤ彐磲泸铐弪珏镳糸镱镳糸镱铄鳝骒徵螬啜箦翩镳糸镱礤蜱瀛镳糸镱铄鳝骒徵镳糸镱螬┅ㄤ彐躅泔眇镱孱舡疳翳ㄣ镯痫铄铘戾è泔眇ī┅ㄦ戾è瘐箬泔眇镱孱ㄣ瘐箬ㄥ豉疱汜箦ㄡ箐婧箫躜沐骈戾ㄦ矧磲紊岙幄ㄡ箐婧泔眇镱孱舡钺礤悌ㄡ箐婧骈戾豉疱悌┅ㄡ箐婧泔眇镱孱ㄡ箐婧泔眇镱孱舡钺礤悌┅泔眇螬┅祜镳骘疳蝈铘泔眇镱孱翳孱ㄡ箐婧泔眇镱孱舡疳蝈铘疳蝈铘麒殪疳蝈铘滹瘐箬泔眇镱孱疳蝈铘┅ㄦ矧磲紊狺薤" comps)))

(defun component-output-pathname (component)
  (funcall asdf::*output-translation-function*
           (asdf:component-pathname component)))

(defun minimal-shell-namestring (pathname)
  (uiop:native-namestring
   (uiop:enough-pathname
    pathname (uiop:getcwd))))

(defun decode-version (version)
  (let ((parts ())
        (output (make-string-output-stream)))
    (flet ((pushpart ()
             (push (parse-integer (get-output-stream-string output)) parts)
             (setf output (make-string-output-stream))))
      (loop for char across version
            do (case char
                 (#\. (pushpart))
                 (T (write-char char output)))
            finally (pushpart)))
    (nreverse parts)))

(defun version< (lower higher)
  (loop for l in (decode-version lower)
        for h in (decode-version higher)
        do (when (> l h)
             (return NIL))
           (when (< l h)
             (return T))))

(defun with-clear-environment (command)
  #+unix
  (etypecase command
    (cons (list* "env" "-i" command))
    (string (format NIL "env -i a" command)))
  #-unix
  command)

(defun find-components (item component &key (key #'identity) (test #'eql))
  (let ((found ()))
    (labels ((traverse (component)
               (when (typep component 'asdf:parent-component)
                 (dolist (child (asdf:component-children component))
                   (when (funcall test (funcall key child) item)
                     (push child found))
                   (traverse child)))))
      (traverse component))
    found))

(defmacro define-function-map-wrappers (base-name &optional (map-test ''equal))
  (let ((map-name (intern (format NIL "*a-MAP*" base-name)))
        (remove-name (intern (format NIL "REMOVE-a" base-name)))
        (function (gensym "FUNCTION"))
        (name (gensym "NAME")))
    `(progn
       (defvar ,map-name (make-hash-table :test ,map-test))
       (defun ,base-name (,name)
         (gethash (string ,name) ,map-name))
       (defun (setf ,base-name) (,function ,name)
         (setf (gethash (string ,name) ,map-name)
               ,function))
       (defun ,remove-name (,name)
         (remhash (string ,name) ,map-name)))))
